plugins {
    id 'java'
    id "com.gradleup.shadow" version "9.3.1"
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'ru.reosfire'
version = "${plugin_version}"

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'dev.folia:folia-api:1.21.11-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'

    implementation 'com.zaxxer:HikariCP:7.0.2'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"
    minimize()

    relocate 'com.zaxxer.hikari', 'ru.reosfire.shaded.com.zaxxer.hikari'

    exclude 'META-INF/**'
}

processResources {
    def props = [
            version: version,
            name: project.name,
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build.dependsOn(shadowJar)